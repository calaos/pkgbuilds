# Maintainer: Raoul Hecky <raoul-arch@calaos.fr>
pkgname=calaos-server
pkgver=3.3
pkgrel=1
pkgdesc="Calaos home automation server service"
url="https://calaos.fr"
license=('GPL')
arch=('x86_64' 'aarch64' 'armv7h')
depends=('libsigc++' 'owfs' 'jansson' 'luajit' 'libusb' 'ola' 'knxd' 'libuv' 'curl' 'sqlite' 'mosquitto' 'imagemagick' 'lua-socket')
makedepends=('pkgconfig' 'libtool' 'autoconf' 'automake' 'git')
provides=("${pkgname}")
conflicts=("${pkgname}")
_pkgdir="calaos_base"

source=('git+https://github.com/calaos/calaos_base.git#commit=2ab947209716afbf332348855682a8253e27864a'
	'calaos-server.service'
  'calaos-server.tmpfiles')
sha256sums=('SKIP'
	    '112fe58ee79efbf2016b0247d8faae599cb6a5c476d0716eb8706ebb0dce1b41'
      '93e6103ebafdb19fc0ab91798ffdcec54ab3ce3e43a916f41186231d32fd6c3e')

prepare() {
  cd "$srcdir/$_pkgdir"
  autopoint --force
  autoreconf -vif
}

build() {
  cd "$srcdir/$_pkgdir"

  #create version file
  cat > src/bin/calaos_server/version.h << EOF
#ifndef VERSION_H
#define VERSION_H
#define PKG_VERSION_STR "$pkgver"
#endif
EOF

  echo $pkgver > version

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_pkgdir"
  make DESTDIR="$pkgdir" install

  install -Dm644 "${srcdir}/../${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm644 "${srcdir}/../${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
