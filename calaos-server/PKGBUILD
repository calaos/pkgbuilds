# Maintainer: Raoul Hecky <raoul-arch@calaos.fr>
pkgname=calaos-server
pkgver=3.3
pkgrel=1
pkgdesc="Calaos home automation server service"
url="https://calaos.fr"
license=('GPL')
arch=('x86_64')
depends=('libsigc++' 'owfs' 'jansson' 'luajit' 'libusb' 'ola' 'knxd' 'libuv' 'curl' 'sqlite' 'mosquitto' 'imagemagick' 'lua-socket')
makedepends=('pkgconfig' 'libtool' 'autoconf' 'automake' 'git')
conflicts=('calaos-server')
replaces=('calaos-server')

source=('git://github.com/calaos/calaos_base.git#commit=2ab947209716afbf332348855682a8253e27864a'
	'calaos-server.service'
        'calaos-server.tmpfiles')
sha256sums=('SKIP'
	    '58b2856a751bff57d816a1e426432209e7ee732324d2998799ed8f8c805d9555'
            '93e6103ebafdb19fc0ab91798ffdcec54ab3ce3e43a916f41186231d32fd6c3e')

prepare() {
  cd "$srcdir/calaos_base"
  autopoint --force
  autoreconf -vif
}

build() {
  cd "$srcdir/calaos_base"
  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/calaos_base"
  make DESTDIR="$pkgdir" install

  install -Dm644 "${srcdir}/../${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm644 "${srcdir}/../${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
