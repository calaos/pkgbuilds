# Maintainer: Raoul Hecky <raoul-arch@calaos.fr>

_pkgname=calaos_mobile
pkgname=calaos-home
pkgver=4.0
pkgrel=1
pkgdesc="Calaos home automation UI service"
url="https://calaos.fr"
license=('GPL')
arch=('x86_64')
depends=('qt6-base' 'qt6-5compat' 'qt6-declarative' 'qt6-imageformats'
         'qt6-svg' 'qt6-virtualkeyboard' 'qt6-webchannel' 'qt6-webengine' 'qt6-websockets' 'qt6-webview')
makedepends=('qt6-tools')
conflicts=('calaos-home')
replaces=('calaos-home')

source=('git://github.com/calaos/calaos_mobile.git#commit=cd0e0961706cdd76b60ffcb1912a07d237478355'
	'calaos-home.service')
sha256sums=('SKIP'
            'f213602bf1b7552a7962d880ce238d4c811d97bc968818979e5d2a975f5e76cb')

prepare() {
  cd "$srcdir/$_pkgname"
  mkdir -p build
}

build() {
  cd "$srcdir/$_pkgname/build"
  qmake6 ../desktop.pro \
      QMAKE_CFLAGS="${CFLAGS}" \
      QMAKE_CXXFLAGS="${CXXFLAGS}"

  make
}

package() {
  cd "$srcdir/$_pkgname/build"

  install -Dm755 "calaos_home" "$pkgdir/usr/bin/calaos_home" 
  install -Dm644 "${srcdir}/../${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
